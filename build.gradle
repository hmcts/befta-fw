plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

apply plugin: 'java'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "9.0.2-SNAPSHOT"

group 'com.github.hmcts'

version buildNumber

def gitRepo = "https://github.com/hmcts/befta-fw"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/befta-fw"
    }
}

// tag::repositories[]
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}
// end::repositories[]

ext {
    junitJupiterVersion = '5.10.2'
    definitionConverterMain = "uk.gov.hmcts.befta.dse.ccd.DefinitionConverter"
}

jar {
    archivesBaseName = "befta-fw"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = false
  testLogging.showStandardStreams = true
  jvmArgs = [
    "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
    "--add-opens=java.base/java.lang=ALL-UNNAMED",
    "--add-opens=java.base/java.util=ALL-UNNAMED"
  ]
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task definitionsToJson(dependsOn: sourcesJar) {
    group = "Execution"
    description = "Runs DefinitionConverter for Excel to JSON"

    doFirst() {
        fileTree("${project.projectDir}/src/main/resources/uk/gov/hmcts/befta/dse/ccd/definitions/excel").matching {
            include "*.xlsx"
            exclude "~*.xlsx"
        }.visit { FileVisitDetails details ->
            def name = details.getName()
            def inputPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/befta/dse/ccd/definitions/excel/${name}"
            def outputFolderPath = "${project.projectDir}/src/main/resources/uk/gov/hmcts/befta/dse/ccd/definitions/valid/"
            def useJurisdictionName = false;

            // overrides for BEFTA Master
            if (name == 'BEFTA_Master_Definition.xlsx') {
                useJurisdictionName = true;
            }

            logger.quiet("\nConverting ${name} to JSON.\n")

            javaexec {
                classpath = sourceSets.main.runtimeClasspath
                main = definitionConverterMain
                args = [
                    "to-json",
                    inputPath,
                    outputFolderPath,
                    useJurisdictionName
                ]
            }
        }
    }

    outputs.upToDateWhen { false }
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'befta-fw'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Backend Functional Test Automation Framework')
                root.appendNode('name', 'BEFTA FW')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

wrapper.gradleVersion = '8.7'

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'

    implementation group: 'io.rest-assured', name: 'rest-assured', version: '5.4.0'

    api group: 'io.cucumber', name: 'cucumber-java', version: '7.18.0'
    api group: 'io.cucumber', name: 'cucumber-junit', version: '7.18.0'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.13'

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.1.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.16'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'

    implementation group: 'com.google.guava', name: 'guava', version: '33.2.0-jre'

    // Purposefully capping the version of org.apache.poi:poi-ooxml to 4.1.2 to avoid issue
    // NoSuchMethodError: org.apache.commons.io.IOUtils.byteArray() from commons-io:commons-io
    implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.3.0'

    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.3.0'
    implementation 'org.springframework.cloud:spring-cloud-openfeign-core:4.1.1'
    implementation 'org.aspectj:aspectjweaver:1.9.22'

    implementation group: 'com.github.rholder', name: 'guava-retrying', version: '2.0.0'

    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.5.0'
    implementation group: 'org.json', name: 'json', version: '20240303'

    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.12.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
    testImplementation group: 'org.junit-pioneer', name: 'junit-pioneer', version: '2.2.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitJupiterVersion}"
    testImplementation  group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${junitJupiterVersion}"
    testRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitJupiterVersion}"
    testRuntimeOnly  group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.10.2'
    testRuntimeOnly  group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.10.2'

}
// end::dependencies[]

task elasticCurlCommands() {
    group = "Help"
    description = "Displays the curl commands required to delete BEFTA case type indexes from ElasticSearch"
    doFirst() {
        getCaseTypes().forEach{
            println "curl -X DELETE http://localhost:9200/${it.toLowerCase()}_cases-000001"
        }
    }
}

def getCaseTypes() {
    def caseTypes = []
    new File("${projectDir}/src/main/resources/uk/gov/hmcts/befta/dse/ccd/definitions/valid/").eachDir { 
        new File(it.path).eachDir {
            if (it.name != 'common') caseTypes << it.name
        }
    }
    // TODO: Hardcode case types which are only available in spreadsheets and add to the list?
    return caseTypes
}

compileJava {
    options.incremental = false
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
